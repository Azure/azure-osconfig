# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT License.

# GenerateSourceDistributionDelegate.cmake
# This CMake script creates a source distribution which consists of the following:
#  * Source package (tar.gz)
#  * sha256 hash the SOURCES files
#  * RPM spec file needed to generate an RPM from the source package

set(BIN_DIR "@CMAKE_BINARY_DIR@")
set(CPACK_PACKAGE_VERSION "@CPACK_PACKAGE_VERSION@")
set(CMAKE_TEMPLATES_DIR "@CMAKE_TEMPLATES_DIR@")

# Project specific variables
set(PROJECT_NAME "@_NAME@")
set(LONG_PROJECT_NAME "@_LONG_NAME@")
set(OUTPUT_DIR "@_OUTPUT_DIR@")
set(SOURCES "@_SOURCES@")

set(STAGING_DIR ${BIN_DIR}/dist)

# Generate spec file
message(STATUS "Generating spec for '${PROJECT_NAME}'.")
configure_file("${CMAKE_TEMPLATES_DIR}${PROJECT_NAME}.spec.in" "${STAGING_DIR}/${PROJECT_NAME}.spec" @ONLY)

# Collect sha256 signatures for each SOURCES file
message(STATUS "Generating sha256 signatures for '${PROJECT_NAME}' SOURCES.")
set(SOURCES_SIGNATURES "")
foreach(src ${SOURCES})
    file(SHA256 "${BIN_DIR}/${src}" SRC_CHECKSUM)
    list(APPEND SOURCES_SIGNATURES "\"${src}\": \"${SRC_CHECKSUM}\"")
endforeach(src)
configure_file("${CMAKE_TEMPLATES_DIR}/projectname.signatures.json.in" "${STAGING_DIR}/${PROJECT_NAME}.signatures.json" @ONLY)

# Copy all assets (SOURCES + spec + signatures) if different from OUTPUT_DIR
foreach(src ${SOURCES})
    execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different "${BIN_DIR}/${src}" "${OUTPUT_DIR}/SOURCES/${src}")
endforeach(src)
execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different "${STAGING_DIR}/${PROJECT_NAME}.spec" "${OUTPUT_DIR}/${PROJECT_NAME}.spec")
execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different "${STAGING_DIR}/${PROJECT_NAME}.signatures.json" "${OUTPUT_DIR}/${PROJECT_NAME}.signatures.json")